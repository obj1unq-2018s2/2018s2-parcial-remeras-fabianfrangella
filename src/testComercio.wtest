import remera.*
import sucursal.*
import comercio.*

describe "test comercio" {

	// Empresa
	var disney = new Empresa(precioPorCopyRight = 100)
	// Dibujos
	var elsa = new DibujoSublimado(alto = 10, ancho = 15, empresa = disney)
	// Remeras
	var remeraLisa = new RemeraLisa(color = "Blanco", talle = 34)
	var remeraBordada = new RemeraBordada(color = "Azul", talle = 38, cantColores = 3)
	var remeraSublimada = new RemeraSublimada(color = "Negro", talle = 42,dibujoSublimado = elsa )
	
	// sucursales
	var avellaneda = new Sucursal(cantidadParaDescuento = 10,acuerdosComerciales = #{})
	var quilmes = new Sucursal(cantidadParaDescuento = 15, acuerdosComerciales = #{disney})
	var gerli = new Sucursal(cantidadParaDescuento = 5, acuerdosComerciales = #{})
	// comercio
	var comercio = new Comercio(sucursales = #{avellaneda,quilmes,gerli})
	// pedidos
	var pedido1 = new Pedido(modelo = remeraLisa, cantidad = 20)
	var pedido2 = new Pedido(modelo = remeraBordada, cantidad = 18)
	var pedido3 = new Pedido(modelo = remeraSublimada,cantidad = 40)
	var pedido4 = new Pedido(modelo = remeraLisa, cantidad = 20)
	var pedidoDisney = new Pedido(modelo = remeraSublimada, cantidad = 20)
	
	
	// Objetos para los tests de los ejemplos
	var remeraLisa1 = new RemeraLisa(color = "Gris", talle = 42)
	var pedidoLisas = new Pedido(modelo = remeraLisa1,cantidad=5)
	var flores = new Sucursal(cantidadParaDescuento = 20,acuerdosComerciales=#{})
	var avellanedaEjemplo = new Sucursal(cantidadParaDescuento = 4, acuerdosComerciales=#{disney})
	var avellanedaSinAcuerdo = new Sucursal(cantidadParaDescuento = 4, acuerdosComerciales=#{})
	var pedidoFrozen = new Pedido(modelo = remeraSublimada,cantidad = 4)
	
	test "registrar un pedido"{
		avellaneda.registrarPedido(pedido1)
		assert.that(avellaneda.pedidos().contains(pedido1))
	}
	
	test "total facturado por el comercio"{
		quilmes.registrarPedido(pedido1)
		assert.equals(comercio.totalFacturado(),1440)
	}
	
	test "cantidad de pedidos de un color"{
		quilmes.registrarPedido(pedido1)
		quilmes.registrarPedido(pedido4)
		assert.equals(comercio.pedidosRealizados("Blanco"),#{pedido1,pedido4})
	}
	
	test "pedido mas caro" {
		gerli.registrarPedido(pedido1)
		gerli.registrarPedido(pedido2)
		gerli.registrarPedido(pedido3)
		gerli.registrarPedido(pedido4)
		assert.equals(gerli.pedidoMasCaro(),pedido3)
	}
	
	test "talles de los que no hubo pedido"{
		gerli.registrarPedido(pedido1)
		gerli.registrarPedido(pedido2)
		assert.equals(comercio.tallesSinPedidosEntre(new Range(42,48)),#{42,43,44,45,46,47,48})
	}
	
	test "sucursal que mas facturo"{
		gerli.registrarPedido(pedido1)
		gerli.registrarPedido(pedido2)
		gerli.registrarPedido(pedido3)
		gerli.registrarPedido(pedido4)
		avellaneda.registrarPedido(pedido1)
		avellaneda.registrarPedido(pedido2)
		assert.equals(comercio.sucursalQueMasFacturo(),gerli)
	}
	
	test "no vendio todos los talles"{
		gerli.registrarPedido(pedido1)
		gerli.registrarPedido(pedido2)
		gerli.registrarPedido(pedido3)
		gerli.registrarPedido(pedido4)
		assert.that(!gerli.vendioTodosLosTalles())
	}
	
	test "sucursales que vendieron todos los talles (ninguna)"{
		assert.equals(comercio.sucursalesQueVendieronTodosLosTalles(),#{})
	}
	
	test "se cambia el costo de las remeras, las vendidas quedan igual"{
		var remera1 = new RemeraLisa (color = "Blanco", talle = 34)
		assert.equals(remera1.costo(),80)
		var pedidoNuevo = new Pedido(modelo = remera1,cantidad = 1)
		gerli.registrarPedido(pedidoNuevo)
		assert.equals(gerli.totalFacturado(),80)
		remera1.cambiarCostoChica(50)
		assert.equals(gerli.totalFacturado(),80)
	}
	
	test "se venden remeras de disney en sucursal con acuerdo y en otra sin acuerdo"{
		quilmes.registrarPedido(pedidoDisney)
		avellaneda.registrarPedido(pedidoDisney)
		assert.equals(quilmes.totalFacturado(),4400)
		assert.equals(avellaneda.totalFacturado(),4950)
	}
	
	// Tests de los ejemplos
	
	/*Un pedido por 5 remeras lisas grises talle 42 tiene un precio de $500 en Flores ($100 de costo de la remera x 5 remeras, no le aplica descuento).
	El mismo pedido en Avellaneda se vende a $450 (idem anterior pero con un 10% de descuento).
	Suponiendo que tenemos acuerdo comercial con Disney y recibimos un pedido de 4 remeras de Frozen antes mencionadas (costo $275) en Flores se venderían a $1100 (= $275 x 4, no hay descuento).
	En Avellaneda, ese mismo pedido costaría un 20% menos ($880).
	Si no tuviéramos acuerdo comercial con Disney, el mismo pedido tendría sólo un 10% de descuento (ergo cuesta $990).
	 */
	 
	 test "test 1"{
	 	flores.registrarPedido(pedidoLisas)
	 	assert.equals(flores.totalFacturado(),500)
	 	avellanedaEjemplo.registrarPedido(pedidoLisas)
	 	assert.equals(avellanedaEjemplo.totalFacturado(),450)
	 }
	 
	 test "test 2"{
	 	avellanedaEjemplo.registrarPedido(pedidoFrozen)
	 	assert.equals(avellanedaEjemplo.totalFacturado(),880)
	 	flores.registrarPedido(pedidoFrozen)
	 	assert.equals(flores.totalFacturado(),1100)	 	
	 }
	 
	 test "test 3"{
	 	avellanedaSinAcuerdo.registrarPedido(pedidoFrozen)
	 	assert.equals(avellanedaSinAcuerdo.totalFacturado(),990)
	 }
}
